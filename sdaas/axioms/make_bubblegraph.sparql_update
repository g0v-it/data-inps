#
# Default metadata for BubbleGraph
#
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bgo: <http://linkeddata.center/lodmap-bgo/v1#>
PREFIX accounts: <https://data.inps.g0v.it/ldp/accounts#>
PREFIX resource: <http://inps.linkeddata.cloud/resource/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX fr: <http://linkeddata.center/botk-fr/v1#>

#
# Set  Bubble Graph title from source dataset
#
INSERT { ?bgo dct:title ?title } 
WHERE {
 	?bgo a bgo:BubbleGraph ; dct:source/dct:title ?title . 
 	
	FILTER NOT EXISTS { ?bgo dct:title  []}
}

;

#
# Set  Bubble Graph description from source dataset
#
INSERT { ?bgo dct:description ?description } 
WHERE {
 	?bgo a bgo:BubbleGraph ; dct:source/dct:description ?description . 
 	
	FILTER NOT EXISTS { ?bgo dct:description  []}
}

;


#
# Create <accounts#upb> partition schema
#
INSERT {
  accounts:upb bgo:partition ?partitionUri.
  ?partitionUri bgo:label ?partitionLabel; bgo:partitionAmount ?partitionAmount.
} WHERE {
  {
    SELECT DISTINCT ?partitionLabel ( SUM(?amount) AS ?partitionAmount) WHERE  {
      ?bubble bgo:partitionLabel ?partitionLabel; bgo:amount ?amount.
      resource:tassonomia_gestionale skos:hasTopConcept/skos:prefLabel ?partitionLabel
    } GROUP BY ?partitionLabel
  }
  BIND( IRI( CONCAT( "https://data.inps.g0v.it/ldp/accounts#upb_partition_",MD5(STR(?partitionLabel)))) AS ?partitionUri )
}


;


#
# Create <accounts#entrate_uscite> partition schema
#
INSERT {
  accounts:entrate_uscite bgo:partition  ?partitionUri.
  ?partitionUri bgo:label ?partitionLabel; bgo:partitionAmount ?partitionAmount.
} WHERE {
  {
	  SELECT DISTINCT ?partitionLabel ( SUM(?amount) AS ?partitionAmount) WHERE  {
	    ?bubble bgo:partitionLabel ?partitionLabel; bgo:amount ?amount.
	    FILTER( ?partitionLabel = "ENTRATE" || ?partitionLabel = "USCITE" )
	  } GROUP BY ?partitionLabel
  }
  BIND( IRI( CONCAT( "https://data.inps.g0v.it/ldp/accounts#partition_",STR(?partitionLabel))) AS ?partitionUri ) 
}


;


#
# Create <accounts#categorie> partition schema
#
INSERT {
  accounts:categorie bgo:partition ?partitionUri.
  ?partitionUri bgo:label ?partitionLabel; bgo:partitionAmount ?partitionAmount.
} WHERE {
  {
    SELECT  ?partitionLabel  ( SUM(?amount) AS ?partitionAmount)  WHERE  {
      ?bubble bgo:partitionLabel ?partitionLabel; bgo:amount ?amount; dct:source/fr:concept ?concept.
      ?concept skos:inScheme resource:tassonomia_gestionale; skos:broader/skos:prefLabel ?partitionLabel.
    } GROUP BY ?partitionLabel
  }
  BIND( IRI( CONCAT( "https://data.inps.g0v.it/ldp/accounts#cat_partition_",MD5(STR(?partitionLabel)))) AS ?partitionUri )
}

